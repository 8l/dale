#|
@module map

Concept macro for a map. Apart from the `Map` macro and concept macro,
the documentation in this module is for a generated set instance
mapping from type `Tk` to type `Tv`.

All of the functions that take `Iterator` arguments are defined for
`ReverseIterator`s as well, notwithstanding that there is no
documentation for those instances.

The `Map` type implements the following concepts:

  * `PairAssociativeContainer`;
  * `SortedAssociativeContainer`; and
  * `UniqueAssociativeContainer`.

Its iterators implement `ForwardIterator`.

|#
(module map (attr cto))

(import cstdio)
(import macros)
(import assert)
(import utility)
(import concepts)
(import derivations)
(import set)
(import algorithms)

#|
@macro Map

Expands to the concrete type name of the `Map` generated by way of
the concept macro.

@param Tk     The key type node.
@param Tv     The value type node.
|#
(def Map
  (macro extern (Tk Tv)
    (let ((typename (array-of 250 char)))
      (and (not (make-type-string "map" Tk Tv typename))
           (return (nullptr DNode)))
      (def n (var auto (p DNode) (std.macros.mnfv mcontext typename)))
      (return n))))

(using-namespace std.macros
(using-namespace std.concepts

#|
@concept-macro Map

Expands to a `Set` definition over the relevant type. Note that `Tk`
must also implement `LessThanComparable`.

@param Tk     The key type node.
@param Tv     The value type node.
|#
(def-concept-macro Map extern ((Tk EqualityComparable) (Tv Type))
  (if (has-errors (qq std.concepts.implements (uq Tk) LessThanComparable))
      (return (qq std.concepts.implements (uq Tk) LessThanComparable))
      0)
 
  (let ((typename       (array-of 250 char))
        (etypename      (array-of 250 char))
        (pairtypename   (array-of 250 char)))
    ; Generate the type names and nodes, and register the main 'map'
    ; type.
    (and (not (make-type-string "map" Tk Tv typename))
         (return (nullptr DNode)))
    (and (not (make-type-string "mapnode" Tk Tv pairtypename))
         (return (nullptr DNode)))
    (and (not (make-type-display-string "Map" Tk Tv etypename))
         (return (nullptr DNode)))

    (register-type typename etypename)

    (def typenode     (var auto (p DNode) 
                                (std.macros.mnfv mcontext typename)))
    (def pairtypenode (var auto (p DNode) 
                                (std.macros.mnfv mcontext pairtypename)))
    
    (qq do

    (using-namespace std.macros
    (using-namespace std.concepts

    ; Has to be instantiated, because there is a wrapper around the
    ; insert function that accepts a 'standard' pair, instead of the
    ; pair constructed for use in the set.
    (instantiate Pair (uq Tk) (uq Tv))

    ; Define the pair value type. Implement the operations required by
    ; EqualityComparable and LessThanComparable, and instantiate a set
    ; for the pair.
    
    (def (uq pairtypenode)
      (struct extern ((first  (uq Tk))
                      (second (uq Tv)))))

    (def = (fn extern bool ((p1 (uq pairtypenode)) 
                            (p2 (uq pairtypenode)))
      (= (@: p1 first)
         (@: p2 first))))

    (def both-= (fn extern bool ((p1 (uq pairtypenode)) 
                                 (p2 (uq pairtypenode)))
      (and (= (@: p1 first)
              (@: p2 first))
           (= (@: p1 second)
              (@: p2 second)))))

    (implement Type (uq pairtypenode))
    (instantiate swap (uq pairtypenode))
    (implement Assignable (uq pairtypenode))
    (instantiate != (uq pairtypenode))

    (def < (fn extern bool ((p1 (uq pairtypenode)) (p2 (uq pairtypenode)))
      (< (@: p1 first)
         (@: p2 first))))

    (mfor F (<= > >=)
      (instantiate F (uq pairtypenode)))

    (implement EqualityComparable (uq pairtypenode))
    (implement LessThanComparable (uq pairtypenode))

    (instantiate Set (uq pairtypenode)) 

    #|
    @struct (Map Tk Tv)

    The core map structure type.

    @linkage extern
    |#
    (def (uq typenode)
      (struct extern ((set (Set (uq pairtypenode))))))

    #|
    @fn init

    Initialise a map.

    @param lst      A map pointer.
    @param-type     (p (Map Tk Tv))
    @linkage        extern
    @return-type    bool
    |#
    (def init (fn extern bool ((mapp (p (uq typenode))))
      (init (:@ mapp set))))

    #|
    @fn size

    Return the number of elements in the map.

    @param mapp     A map pointer.
    @param-type     (p (Map Tk Tv))
    @linkage        extern
    @return-type    size
    |#
 
    (def size (fn extern size ((mapp (p (uq typenode))))
      (size (:@ mapp set))))

    #|
    @fn max-size

    Returns the number of elements that can be accommodated by the
    map.

    @param mapp     A map pointer.
    @param-type     (p (Map Tk Tv))
    @linkage        extern
    @return-type    size
    |#
    (def max-size (fn extern size ((mapp (p (uq typenode))))
      (max-size (:@ mapp set))))

    #|
    @fn empty

    Determine whether the map is empty.

    @param lst      A map pointer.
    @param-type     (p (Map Tk Tv))
    @linkage        extern
    @return-type    bool
    |#
    (def empty (fn extern bool ((mapp (p (uq typenode))))
      (empty (:@ mapp set))))

    #|
    @macro key-type

    Expands to the underlying key type of the map (i.e. `Tk`). This
    only uses the type node for dispatch purposes, so it's safe to
    call this with e.g. `(nullptr (Map Tk Tv))` as the argument.

    @param lst      A type node.
    @param-type     (p (Map Tk Tv))
    @linkage        extern
    |#
    (def key-type
      (macro extern ((mapp (p (uq typenode))))
        (qq do (uq Tk))))

    #|
    @macro data-type

    Expands to the underlying data type (i.e. `Tv`) of the map.

    @param lst      A type node.
    @param-type     (p (Map Tk Tv))
    @linkage        extern
    |#
    (def data-type
      (macro extern ((mapp (p (uq typenode))))
        (qq do (uq Tv))))

    #|
    @macro value-type

    Expands to the underlying value type of the set. Note that for
    maps, the value type is a specially-constructed pair type that is
    not otherwise accessible. However, it is guaranteed to be a struct
    that has the members `first` and `second`, with those members
    mapping to `Tk` and `Tv` respectively.

    @param lst      A type node.
    @param-type     (p (Map Tk Tv))
    @linkage        extern
    |#
    (def value-type
      (macro extern ((mapp (p (uq typenode))))
        (qq do (uq pairtypenode))))

    #|
    @macro size-type

    Expands to the underlying size type of the map.

    @param lst      A type node.
    @param-type     (p (Map Tk Tv))
    @linkage        extern
    |#
    (def size-type
      (macro extern ((mapp (p (uq typenode))))
        (qq do size)))

    #|
    @macro difference-type

    Expands to the underlying iterator difference type of the map.

    @param lst      A type node.
    @param-type     (p (Map Tk Tv))
    @linkage        extern
    |#
    (def difference-type
      (macro extern ((mapp (p (uq typenode))))
        (qq do ptrdiff)))

    #|
    @struct (Iterator (Map Tk) (Map Tv))

    @linkage extern
    |#
    (def (Iterator (Map (uq Tk) (uq Tv)))
      (struct extern ((mapp (p (uq typenode)))
                      (setiter (Iterator (Set (uq pairtypenode)))))))

    #|
    @macro value-type

    Expands to the underlying value type of the iterator. This is the
    same as that of `value-type` for the container.

    @param iter     A type node.
    @param-type     (p (Iterator (Map Tk Tv)))
    @linkage        extern
    |#
    (def value-type
      (macro extern ((mapiter (p (Iterator (Map (uq Tk) (uq Tv))))))
        (qq do (uq pairtypenode))))

    #|
    @fn end

    Returns the iterator representing the end of the map (sentinel).

    @param lst      A set pointer.
    @param-type     (p (Map Tk Tv))
    @linkage        extern
    @return-type    (Iterator (Map Tk Tv))
    |#
    (def end (fn extern (Iterator (Map (uq Tk) (uq Tv))) 
                            ((mapp (p (uq typenode))))
      ((Iterator (Map (uq Tk) (uq Tv))) 
        ((mapp mapp)
         (setiter (end (:@ mapp set)))))))

    #|
    @fn begin

    Returns the iterator for the first map element.

    @param lst      A set pointer.
    @param-type     (p (Map Tk Tv))
    @linkage        extern
    @return-type    (Iterator (Map Tk Tv))
    |#
    (def begin (fn extern (Iterator (Map (uq Tk) (uq Tv))) 
                            ((mapp (p (uq typenode))))
      ((Iterator (Map (uq Tk) (uq Tv))) 
        ((mapp    mapp)
         (setiter (begin (:@ mapp set)))))))

    #|
    @fn @source

    Returns the iterator's value.

    @param iter     An iterator.
    @param-type     (Iterator (Map Tk Tv))
    @linkage        extern
    @return-type    T
    |#
    (def @source (fn extern (uq pairtypenode)
                           ((iter (Iterator (Map (uq Tk) (uq Tv)))))
      (@source (@: iter setiter))))

    #|
    @fn source

    Returns a pointer to the iterator's value.

    @param iter     An iterator.
    @param-type     (Iterator (Map Tk Tv))
    @linkage        extern
    @return-type    (p T)
    |#
    (def source (fn extern (p (uq pairtypenode))
                           ((iter (Iterator (Map (uq Tk) (uq Tv)))))
      (source (@: iter setiter))))

    #|
    @fn successor

    Returns the iterator for the position that follows the argument
    iterator.

    @param iter     An iterator.
    @param-type     (Iterator (Map Tk Tv))
    @linkage        extern
    @return-type    (Iterator (Map Tk Tv))
    |#
    (def successor (fn extern (Iterator (Map (uq Tk) (uq Tv)))
                              ((iter (Iterator (Map (uq Tk) (uq Tv)))))
      ((Iterator (Map (uq Tk) (uq Tv))) 
        ((mapp    (@: iter mapp))
         (setiter (successor (@: iter setiter)))))))

    #|
    @fn =

    @param iter1    The first iterator.
    @param-type     (Iterator (Map Tk Tv))
    @param iter2    The second iterator.
    @param-type     (Iterator (Map Tk Tv))
    @linkage        extern
    @return-type    bool
    |#
    (def = (fn extern bool ((a (Iterator (Map (uq Tk) (uq Tv))))
                            (b (Iterator (Map (uq Tk) (uq Tv)))))
      (and (p= (@: a mapp)    (@: b mapp))
           (=  (@: a setiter) (@: b setiter)))))

    (implement Type (Iterator (Map (uq Tk) (uq Tv))))
    (instantiate != (Iterator (Map (uq Tk) (uq Tv))))
    (instantiate swap (Iterator (Map (uq Tk) (uq Tv))))

    #|
    @fn <

    @param iter1    The first iterator.
    @param-type     (Iterator (Map Tk Tv))
    @param iter2    The second iterator.
    @param-type     (Iterator (Map Tk Tv))
    @linkage        extern
    @return-type    bool
    |#
    (def < (fn extern bool ((a (Iterator (Map (uq Tk) (uq Tv))))
                            (b (Iterator (Map (uq Tk) (uq Tv)))))
      (and (p= (@: a mapp)    (@: b mapp))
           (<  (@: a setiter) (@: b setiter)))))

    (mfor F (<= > >=)
      (instantiate F (Iterator (Map (uq Tk) (uq Tv)))))
    (implement LessThanComparable (Iterator (Map (uq Tk) (uq Tv))))

    #|
    @struct (ReverseIterator (Map Tk Tv))

    @linkage extern
    |#
    (def (ReverseIterator (Map (uq Tk) (uq Tv)))
      (struct extern ((mapp (p (uq typenode)))
                      (setiter (ReverseIterator (Set (uq pairtypenode)))))))

    #|
    @fn rend

    Returns the iterator representing the beginning of the map (sentinel).

    @param lst      A map pointer.
    @param-type     (p (Map Tk Tv))
    @linkage        extern
    @return-type    (ReverseIterator (Map Tk Tv))
    |#
    (def rend (fn extern (ReverseIterator (Map (uq Tk) (uq Tv))) 
                            ((mapp (p (uq typenode))))
      ((ReverseIterator (Map (uq Tk) (uq Tv))) ((mapp mapp)
                           (setiter (rend (:@ mapp set)))))))

    #|
    @fn rbegin

    Returns the iterator for the last map element.

    @param lst      A set pointer.
    @param-type     (p (Map Tk Tv))
    @linkage        extern
    @return-type    (ReverseIterator (Map Tk Tv))
    |#
    (def rbegin (fn extern (ReverseIterator (Map (uq Tk) (uq Tv))) 
                            ((mapp (p (uq typenode))))
      ((ReverseIterator (Map (uq Tk) (uq Tv))) ((mapp mapp)
                           (setiter (rbegin (:@ mapp set)))))))

    (def value-type
      (macro extern ((mapiter (p (ReverseIterator (Map (uq Tk) (uq Tv))))))
        (qq do (uq pairtypenode))))

    (def source (fn extern (p (uq pairtypenode))
                           ((iter (ReverseIterator (Map (uq Tk) (uq Tv)))))
      (source (@: iter setiter))))

    (def @source (fn extern (uq pairtypenode)
                            ((iter (ReverseIterator (Map (uq Tk) (uq Tv)))))
      (@source (@: iter setiter))))

    (def successor (fn extern (ReverseIterator (Map (uq Tk) (uq Tv)))
                              ((iter (ReverseIterator 
                                            (Map (uq Tk) (uq Tv)))))
      ((ReverseIterator (Map (uq Tk) (uq Tv))) 
        ((mapp    (@: iter mapp))
         (setiter (successor (@: iter setiter)))))))

    (def = (fn extern bool ((a (ReverseIterator (Map (uq Tk) (uq Tv))))
                            (b (ReverseIterator (Map (uq Tk) (uq Tv)))))
      (and (p= (@: a mapp)    (@: b mapp))
           (=  (@: a setiter) (@: b setiter)))))

    (implement Type (ReverseIterator (Map (uq Tk) (uq Tv))))
    (instantiate != (ReverseIterator (Map (uq Tk) (uq Tv))))
    (instantiate swap (ReverseIterator (Map (uq Tk) (uq Tv))))

    (def < (fn extern bool ((a (ReverseIterator (Map (uq Tk) (uq Tv))))
                            (b (ReverseIterator (Map (uq Tk) (uq Tv)))))
      (and (p= (@: a mapp)    (@: b mapp))
           (>= (@: a setiter) (@: b setiter)))))

    (mfor F (<= > >=)
      (instantiate F (ReverseIterator (Map (uq Tk) (uq Tv)))))
    (implement LessThanComparable (ReverseIterator (Map (uq Tk) (uq Tv))))

    #|
    @fn clear

    Remove all of the elements from the map.

    @param setp     The map pointer.
    @param-type     (p (Map Tk Tv))
    @linkage        extern
    @return-type    bool
    |#
    (def clear (fn extern void ((mapp (p (uq typenode))))
      (clear (:@ mapp set))))

    #|
    @fn insert

    Insert a new element into the map.

    @param setp     A map pointer.
    @param-type     (p (Map Tk Tv))
    @param val      The value to insert into the map.
    @param-type     (value-type (nullptr (Map Tkey Tval)))
    @linkage        extern
    @return-type    bool
    |#
    (def insert (fn extern bool ((mapp (p (uq typenode)))
                                 (valt (uq pairtypenode)))
      (insert (:@ mapp set) valt)))

    #|
    @fn insert

    Insert a new element into the map.

    @param setp     A map pointer.
    @param-type     (p (Map Tk Tv))
    @param val      The value to insert into the set.
    @param-type     (Pair Tk Tv)
    @linkage        extern
    @return-type    bool
    |#
    (def insert (fn extern bool ((mapp (p (uq typenode)))
                                 (valt (Pair (uq Tk) (uq Tv))))
      (insert mapp (@ (cast (# valt) (p (uq pairtypenode)))))))

    #|
    @fn find

    Find an element within the map, and return the iterator for its
    position.

    @param setp     A map pointer.
    @param-type     (p (Map Tk Tv))
    @param val      The value to find in the set.
    @param-type     Tk
    @linkage        extern
    @return-type    (Iterator (Map Tk Tv))
    |#
    (def find (fn extern (Iterator (Map (uq Tk) (uq Tv))) 
                              ((mapp (p (uq typenode)))
                               (key  (uq Tk)))
      (let ((temp (uq pairtypenode)))
        (setf (: temp first) key)
        (let ((myiter \ (find (:@ mapp set) temp))
              (itn    (Iterator (Map (uq Tk) (uq Tv)))))
          (setf (: itn mapp) mapp)
          (setf (: itn setiter) myiter)
          itn))))

    #|
    @fn erase

    Erase an element from the map, by specifying the iterator for its
    position.

    @param iter     The iterator.
    @param-type     (Iterator (Map Tk Tv))
    @linkage        extern
    @return-type    bool
    |#
    (def erase (fn extern bool ((iter (Iterator (Map (uq Tk) (uq Tv)))))
      (erase (@: iter setiter))))

    #|
    @fn erase

    Erase an element from the map by value.

    @param setp     A map pointer.
    @param-type     (p (Map Tk Tv))
    @param val      The value to remove from the set.
    @param-type     Tk
    @linkage        extern
    @return-type    bool
    |#
    (def erase (fn extern bool ((mapp (p (uq typenode))) 
                                (key (uq Tk)))
      (erase (@: (find mapp key) setiter))))

    #|
    @fn count

    Return the number of times that the value appears in the map. For
    a `UniqueAssociativeContainer` such as `Map`, this can only return
    1 or 0, depending on whether the element is present in the set.

    @param setp     A map pointer.
    @param-type     (p (Map Tk Tv))
    @param val      The value for which the count should be determined.
    @param-type     Tk
    @linkage        extern
    @return-type    size
    |#
    (def count (fn extern size ((mapp (p (uq typenode))) 
                                (key (uq Tk)))
      (let ((f \ (find mapp key)))
        (cast (if (= f (end mapp)) 0 1) size))))

    #|
    @fn lower-bound

    Find the lower bound for a given value.

    @param setp     A map pointer.
    @param-type     (p (Map Tk Tv))
    @param val      The value for which the lower bound should be found.
    @param-type     Tk
    @linkage        extern
    @return-type    (Iterator (Map Tk Tv))
    |#
    (def lower-bound (fn extern (Iterator (Map (uq Tk) (uq Tv)))
                                  ((mapp (p (uq typenode)))
                                   (key  (uq Tk)))
      (let ((temp (uq pairtypenode)))
        (setf (: temp first) key)
        (let ((lb \ (lower-bound (:@ mapp set) temp)))
          ((Iterator (Map (uq Tk) (uq Tv))) ((mapp mapp)
                              (setiter lb)))))))

    #|
    @fn upper-bound

    Find the upper bound for a given value.

    @param setp     A map pointer.
    @param-type     (p (Map Tk Tv))
    @param val      The value for which the upper bound should be found.
    @param-type     Tk
    @linkage        extern
    @return-type    (Iterator (Map Tk Tv))
    |#
    (def upper-bound (fn extern (Iterator (Map (uq Tk) (uq Tv)))
                                  ((mapp (p (uq typenode)))
                                   (key  (uq Tk)))
      (let ((temp (uq pairtypenode)))
        (setf (: temp first) key)
        (let ((lb \ (upper-bound (:@ mapp set) temp)))
          ((Iterator (Map (uq Tk) (uq Tv))) ((mapp mapp)
                              (setiter lb)))))))

    (def setf-copy (fn extern bool ((dst (p (uq typenode)))
                                    (src (p (uq typenode))))
      (init (:@ dst set))
      (let ((b \ (begin (:@ src set)))
            (e \ (end   (:@ src set))))
        (for true (!= b e) (setv b (successor b))
          (insert (:@ dst set) (@source b))))
      true))

    (def setf-assign (fn extern bool ((dst (p (uq typenode)))
                                      (src (p (uq typenode))))
      (clear (:@ dst set))
      (setf-copy dst src)))

    (implement Type (uq typenode))
    (instantiate swap (uq typenode))

    (def = (fn extern bool ((map1 (p (uq typenode)))
                            (map2 (p (uq typenode))))
      (let ((b1 \ (begin (:@ map1 set)))
            (b2 \ (begin (:@ map2 set)))
            (e1 \ (end   (:@ map1 set)))
            (e2 \ (end   (:@ map2 set))))
        (for true (and (!= b1 e1)
                       (!= b2 e2))
                  (do (setv b1 (successor b1))
                      (setv b2 (successor b2)))
          (let ((v1 \ (@source b1))
                (v2 \ (@source b2)))
            (and (not (both-= v1 v2))
                 (return false))))
        (and (= b1 e1) (= b2 e2)))))

    (def < (fn extern bool ((map1 (p (uq typenode)))
                            (map2 (p (uq typenode))))
      (< (@:@ map1 set) (@:@ map2 set))))

    (implement Type (p (uq typenode)))
    (mfor F (!= <= > >=)
      (instantiate F (p (uq typenode))))

    (mfor F (= != < <= > >=)
      (def F (fn extern bool ((map1 (uq typenode))
                              (map2 (uq typenode)))
        (F (# map1) (# map2)))))

    (def destroy
      (fn extern void ((mapp (p (uq typenode))))
        (clear mapp)
        (return)))

    (implement EqualityComparable (uq typenode))
    (implement LessThanComparable (uq typenode))
    (implement ForwardIterator (Iterator (Map (uq Tk) (uq Tv))))
    (implement ForwardIterator (ReverseIterator (Map (uq Tk) (uq Tv))))
    (implement SortedAssociativeContainer (uq typenode))
    (implement UniqueAssociativeContainer (uq typenode))
    (implement PairAssociativeContainer (uq typenode))
    ))
)))))
